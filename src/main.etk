%macro push0()
        # TODO: replace with push0 when it's added
        push1 0
%end

%def historical_roots_modulus()
        98304
%end

%macro function_sig() 
        push4 0x9507d39a
%end

%macro get_input()
        push1 4
        calldataload
%end

%macro get_timestamp_index()
        push3 historical_roots_modulus()
        timestamp
        mod
%end
        
# If caller is from system, jump to update.
caller
push20 0xfffffffffffffffffffffffffffffffffffffffe
eq
push1 update
jumpi

########
# READ #
########

# Check if calldata has the function signature
push1 0         # [0]
calldataload    # [calldataload]
push1 224
shr
%function_sig() # []
eq
not
push1 revert    # [revert_addr, calldatasize < 32]
jumpi           # []

# Load stored timestamp.
push3 historical_roots_modulus() # [hrm, time]
%get_input()    # [time]
mod             # [time_index]
dup1            # [time_index, time_index]
sload           # [want, time_index]

# Verify stored timestamp matches input.
%get_input() # [got, want, time_index]
eq              # [got == want, time_index]
iszero          # [got != want, time_index]
push1 revert    # [revert_addr, got != want, time_index]
jumpi           # [time_index]

# Extend index to get root index.
push3 historical_roots_modulus() # [hsm, time_index]
add             # [root_index]
sload           # [root]

%push0()        # [0, root]
mstore          # []

push1 32        # [size]
%push0()        # [offset]
return

##########
# UPDATE #
##########

update:
jumpdest
timestamp               # [time]
%get_timestamp_index()  # [time_index, time]
sstore                  # []

%push0()                # [0]
calldataload            # [root]
%get_timestamp_index()  # [time_index, root]
push3 historical_roots_modulus() # [hsm, time_index, root]
add                     # [root_index, root]
sstore

%push0()
%push0()
return

##########
# REVERT #
##########

revert:
jumpdest
%push0()
%push0()
revert
